include "inc/cmdsys.plh"
include "inc/fileio.plh"

struc t_diskinfo
    word codelen
    word codeaddr
end
struc t_codefile
    byte[16*t_diskinfo] diskinfo
    byte[16*8]          segname
    word[16]            segkind
    word[16]            textaddr
    word[16]            seginfo
end

export word[64] sane
byte ref
word pcode, fp6502, elems, preloc

asm equates
        !SOURCE "vmsrc/plvmzp.inc"
end
//
// PLASMA <-> SANE interface routines
//
asm fpOp1(op, dst)#1
        LDA     ESTKH,X
        PHA
        LDA     ESTKL,X
        PHA
        LDA     ESTKH+1,X
        PHA
        LDA     ESTKL+1,X
        PHA
        INX
        STX     ESP
end
asm fixupFP1
        JSR     $FFFF
        TXA
        LDX     ESP
        STA     ESTKL,X
        STY     ESTKH,X
        RTS
end
asm fpOp2(op1, dst, src)#1
        LDA     ESTKH,X
        PHA
        LDA     ESTKL,X
        PHA
        LDA     ESTKH+1,X
        PHA
        LDA     ESTKL+1,X
        PHA
        LDA     ESTKH+2,X
        PHA
        LDA     ESTKL+2,X
        PHA
        INX
        INX
        STX     ESP
end
asm fixupFP2
        JSR     $FFFF
        TXA
        LDX     ESP
        STA     ESTKL,X
        STY     ESTKH,X
        RTS
end
def prbyte(h)#0
    putc('$')
    call($FDDA, h, 0, 0, 0)
end
def prword(h)#0
    putc('$')
    call($F941, h >> 8, h, 0, 0)
end
def print(i)#0
    byte numstr[7]
    byte place, sign

    place = 6
    if i < 0
        sign = 1
        i    = -i
    else
        sign = 0
    fin
    while i >= 10
        numstr[place] = i % 10 + '0'
        i             = i / 10
        place         = place - 1
    loop
    numstr[place] = i + '0'
    place         = place - 1
    if sign
        numstr[place] = '-'
        place         = place - 1
    fin
    numstr[place] = 6 - place
    puts(@numstr[place])
end
def putname(pchr)#0
    byte c

    for c = 0 to 7
        putc(pchr->[c])
    next
end
def putln#0
    putc('\n')
end
def dumpheader(phdr)#0
    byte i

    puts("Seg Info\n")
    for i = 0 to 15
        if (phdr + i * t_diskinfo)=>codelen
            prword((phdr + i * t_diskinfo)=>codelen)
            putc(':')
            prword((phdr + i * t_diskinfo)=>codeaddr)
            putc('=')
            putname(phdr + i * 8 + segname)
            putc(',')
            prword((phdr + segkind)=>[i])
            putc(',')
            prword((phdr + textaddr)=>[i])
            putc(',')
            prword((phdr + seginfo)=>[i])
            putln
        fin
    next
    putname(phdr + $01F4); putln
end
def reloc(base, prel, ofst)
    word listsz, list, len

    list = prel
    while prel->1 == 1
        prel = prel - 2
        listsz = *prel
        while listsz
            prel = prel - 2
            if *prel
                list = base + *prel - ofst
                len = *list
                list = list - 2
                puts("Reloc list len = "); prword(len); putln
                while len
                    *(list - *list) = *(list - *list) + base
                    list = list - 2
                    len--
                loop
            fin
            listsz--
        loop
    loop
    return list
end
def fixup(base, pli, preloc)
    byte fixups
    word basend

    puts("LinkInfo "); prword(pli); putc(':'); putln
    basend = preloc
    while ^pli
        putname(pli); putc(':')
        prword(pli=>8); putc(' ')
        prword(pli=>10); putc(' ')
        prword(pli=>12); putc(' ')
        prword(pli=>14); putln
        fixups = 0
        if pli=>8 == $0002
            fixups = pli=>12
        elsif pli=>8 == $0006 and pli=>10 == $0001
            basend = reloc(base, preloc, pli=>12)
        fin
        pli = pli + 16
        while fixups
            *(base + *pli) = fp6502
            pli = pli + 2
            fixups--
        loop
    loop
end
def loadcode(codefile)
    byte ref
    word pcode, preloc, pli

    puts(codefile); puts(":\n")
    pcode = 0
    ref   = open(codefile, sysbuf)
    if ref
        pcode = heapmark
        read(ref, pcode, 512)
        dumpheader(pcode)
        putname(pcode + segname + 8); putc('='); prword(pcode); putln
        preloc = (pcode + t_diskinfo)=>codeaddr
        read(ref, pcode, heapavail)
        close(ref)
        pli    = pcode + (preloc | 511) + 1
        preloc = pcode + preloc - 2
        puts("Reloc = "); prword(preloc); putln
        heaprelease(fixup(pcode, pli, preloc)) // Set heap to beginning of relocation list
    fin
    return pcode
end
fp6502 = loadcode("FP6502.CODE")
if !fp6502
    puts("SANE library not found.\n")
    return -1
fin
elems = loadcode("ELEMS.CODE")
if !elems
    puts("ELEMS library not found.\n")
    return -1
fin
call(-151, 0, 0, 0, 0)
return 0
done
