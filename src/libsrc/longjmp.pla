asm incs
        !SOURCE    "vmsrc/plvmzp.inc"
end
//
// Save environment (PLASMA ZP and stack) for below and return 0
//
export asm setjmp(env)
        LDA     ESTKL,X
        STA     SRC
        LDA     ESTKH,X
        STA     SRC+1
        STX     ESP
        TSX
        STX     TMPL
        LDX     #ESTK
        LDY     #$00
-       LDA     $00,X
        STA     (SRC),Y
        INY
        INX
        BNE     -
-       LDA     $0100,X
        STA     (SRC),Y
        INY
        BNE     +
        INC     SRC+1
+       INX
        BNE     -
        TXA
        LDX     ESP
        STA     ESTKL,X
        STA     ESTKH,X
        RTS
end
//
// Restore environment saved above and return retval
//
export asm longjmp(env, retval)
        LDA     ESTKL,X
        STA     SRC
        LDA     ESTKH,X
        STA     SRC+1
        LDA     ESTKL+1,X
        STA     DST
        LDA     ESTKH+1,X
        STA     DST+1
        LDX     #ESTK
        LDY     #$00
-       LDA     (DST),Y
        STA     $00,X
        INY
        INX
        BNE     -
-       LDA     (DST),Y
        STA     $0100,X
        INY
        BNE     +
        INC     DST+1
+       INX
        BNE     -
        LDX     TMP
        TXS
        LDX     ESP
        LDA     SRC
        STA     ESTKL,X
        LDA     SRC+1
        STA     ESTKH,X
        RTS
end
